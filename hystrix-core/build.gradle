apply plugin: 'osgi'
apply plugin: 'me.champeau.gradle.jmh'
apply plugin: 'maven'

dependencies {
    compileApi 'com.netflix.archaius:archaius-core:0.4.1'
    compileApi 'io.reactivex:rxjava:1.2.0'
    compile 'org.slf4j:slf4j-api:1.7.0'
    compileApi 'org.hdrhistogram:HdrHistogram:2.1.9'
    testCompile 'junit:junit-dep:4.10'
    testCompile project(':hystrix-junit')
}


javadoc {
    // the exclude isn't working, nor is there a subPackages options as docs suggest there should be
    // we do not want the com.netflix.hystrix.util package include
    exclude '**/util/**'

    options {
        doclet = "org.benjchristensen.doclet.DocletExclude"
        docletpath = [rootProject.file('./gradle/doclet-exclude.jar')]
        stylesheetFile = rootProject.file('./gradle/javadocStyleSheet.css')
        windowTitle = "Hystrix Javadoc ${project.version}"
    }
    options.addStringOption('top').value = '<a href="https://github.com/Netflix/Hystrix"><img width="92" height="79" border="0" align="left" src="http://netflix.github.com/Hystrix/images/hystrix-logo-small.png"></a><h2 class="title" style="padding-top:40px">Hystrix: Latency and Fault Tolerance for Distributed Systems</h2>'
}

jar {
    manifest {
        name = 'hystrix-core'
        instruction 'Bundle-Vendor', 'Netflix'
        instruction 'Bundle-DocURL', 'https://github.com/Netflix/Hystrix'
        instruction 'Import-Package', '!org.junit,!junit.framework,!org.mockito.*,*'
        instruction 'Eclipse-ExtensibleAPI', 'true'
        instruction 'Embed-Dependency', '*;scope=compile'
    }
}

jmh {
    fork = 10
    iterations = 3
    jmhVersion = '1.15'
    profilers = ['gc']
    threads = 8
    warmup = '1s'
    warmupBatchSize = 1
    warmupIterations = 5
}
task createPom << {
    pom {
        project {
            groupId 'com.netflix'
            artifactId 'hystrix-core'
            version '1.0.0'

            inceptionYear '2008'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }.writeTo("pom.xml")
}
